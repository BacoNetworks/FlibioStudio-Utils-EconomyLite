plugins {
    id 'java'
    id 'maven'
    id 'net.minecrell.licenser' version '0.1.5'
}

group 'io.github.flibio'

defaultTasks 'clean', 'licenseFormat', 'commands:build', 'file:build', 'message:build', 'player:build', 'sql:build'

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven'
    
    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    repositories {
        mavenCentral()
        jcenter()
        maven {
            name 'Sponge API repo'
            url 'http://repo.spongepowered.org/maven'
        }
        maven {
            name = 'minecrell-releases'
            url = 'http://repo.minecrell.net/releases'
        }
    }
    
    dependencies {
        compile "org.spongepowered:spongeapi:4.1.0-SNAPSHOT"
    }
    
    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }
    
    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }
    
    artifacts {
        archives sourcesJar
        archives javadocJar
    }
}

project(':message') { 
    dependencies {
        compile project(':file')
    }
}

license {
    ext {
        name = project.name
        organization = project.organization
        url = project.url
        inceptionYear = project.inceptionYear
        currentYear = project.currentYear
    }
    
    header = rootProject.file('HEADER.txt')
    include '**/*.java'
}

task wrapper(type: Wrapper) {
  gradleVersion = '2.12'
}
